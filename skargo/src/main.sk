module Skargo;

fun main(): void {
  cmd = Cli.Command("skargo")
    .about("Skip's package manager")
    .arg(
      Cli.BoolArg("version")
        .short("V")
        .long("version")
        .about("Print version info and exit"),
    )
    .arg(
      Cli.BoolArg("verbose")
        .short("v")
        .long("verbose")
        .about("Use verbose output")
        .global(),
    );
  suppliers = Array[
    check,
    build,
    test,
    runner,
    clean,
    format,
    init,
    update,
    help,
  ];
  commands = mutable Map[];
  for (supplier in suppliers) {
    (subcommand, cmdFn) = supplier();
    !cmd = cmd.subcommand(subcommand);
    commands.set(subcommand.name, cmdFn);
  };
  !cmd = cmd.help();
  args = cmd.parseArgs();
  if (args.getBool("version")) {
    // FIXME
    // #env("SKARGO_PKG_VERSION")
    print_string("skargo " + "FIXME");
  } else {
    args.maybeGetSubcommand() match {
    | Some(subcmd) ->
      commands.maybeGet(subcmd) match {
      | Some(handler) -> handler(args)
      | _ -> invariant_violation(`Unknown subcommand ${subcmd}`)
      }
    | None() -> print_string(Cli.usage(args.cmd, true))
    }
  }
}

fun gctxCmd(
  args: Cli.ParseResults,
  fn: (GlobalContext, Cli.ParseResults) ~> void,
): void {
  gctx = GlobalContext::create(args);
  fn(gctx, args)
}

module end;
