module Skargo;

private class Package(
  manifest: Manifest,
  manifest_path: String,
) uses Equality, Hashable, Show {
  static fun read(manifest_path: String): Package {
    Package(Manifest::read(manifest_path), manifest_path)
  }

  fun name(): String {
    this.manifest.package_id.name
  }

  fun version(): Semver.Version {
    this.manifest.package_id.version
  }

  fun ==(other: Package): Bool {
    this.manifest.package_id == other.manifest.package_id
  }

  fun root(): String {
    Path.dirname(this.manifest_path)
  }

  fun hash(): Int {
    // TODO: Proper hash
    this.manifest.package_id.hash()
  }

  fun toString(): String {
    `${this.manifest.package_id.name} v${
      this.manifest.package_id.version
    } (${this.root()})`
  }
}

class TargetTriple(
  arch: String,
  vendor: ?String,
  sys: ?String,
  abi: ?String,
) uses Orderable, Hashable, Show {
  static fun fromString(str: String): TargetTriple {
    components = str.split("-");
    if (components.isEmpty() || components.size() > 4) {
      invariant_violation("Invalid target triplet string " + str)
    };
    TargetTriple(
      components[0],
      components.maybeGet(1) match {
      | Some(v) if (v != "unknown") -> Some(v)
      | _ -> None()
      },
      components.maybeGet(2) match {
      | Some(v) if (v != "unknown") -> Some(v)
      | _ -> None()
      },
      components.maybeGet(3) match {
      | Some(v) if (v != "unknown") -> Some(v)
      | _ -> None()
      },
    )
  }

  fun isWasm32(): Bool {
    this.arch == "wasm32"
  }

  fun toString(): String {
    `${this.arch}-${this.vendor.default("unknown")}-${this.sys.default(
      "unknown",
    )}` +
      this.abi match {
      | Some(v) -> `-${v}`
      | None() -> ""
      }
  }
}

private base class TargetArch uses Equality, Hashable, Show {
  children =
  | TargetArchHost()
  | TargetArchTriple(TargetTriple)

  fun toString(): String
  | TargetArchHost() -> "host"
  | TargetArchTriple(triple) -> triple.toString()
}

private class BuildContext{
  ws: Workspace,
  gctx: GlobalContext,
  build_config: BuildConfig,
  roots: Array<Unit>,
  unit_graph: UnitGraph,
  target_dir: String,
  skc: Skc,
}

// TODO: Return Result<...>
fun create_bctx(
  gctx: GlobalContext,
  ws: Workspace,
  opts: CompileOptions,
): BuildContext {
  resolved_packages = resolve(ws.package);

  roots = generate_root_units(ws.package, opts.filter, opts.build_config);
  unit_graph = build_unit_dependencies(roots, resolved_packages);
  skc = Skc(Environ.varOpt("SKC").default("skc"));
  target_dir = ws.target_dir();
  BuildContext{
    ws,
    gctx,
    build_config => opts.build_config,
    roots,
    unit_graph,
    target_dir,
    skc,
  }
}

private fun generate_root_units(
  package: Package,
  filter: CompileFilter,
  build_config: BuildConfig,
): Array<Unit> {
  package.manifest.targets
    .filter(target ->
      filter match {
      | CompileFilterDefault() ->
        target.is_bin() || target.is_lib() || target.is_test()
      | CompileFilterOnly{lib} if (target.is_lib()) -> !(lib is LibRuleFalse _)
      | CompileFilterOnly{bins} if (target.is_bin()) -> bins.matches(target)
      | CompileFilterOnly{tests} if (target.is_test()) -> tests.matches(target)
      | _ -> false
      }
    )
    .map(target ->
      Unit(package, target, build_config.requested_arch, CompileModeBuild())
    )
    .collect(Array)
}

fun build_unit_dependencies(
  roots: Array<Unit>,
  resolved_packages: Map<String, Package>,
): UnitGraph {
  unit_graph = mutable UnitGraph[];
  for (unit in roots) {
    build_unit_dependencies_for_unit(unit, resolved_packages, unit_graph)
  };

  unit_graph.chill()
}

private fun build_unit_dependencies_for_unit(
  unit: Unit,
  resolved_packages: Map<String, Package>,
  unit_graph: mutable UnitGraph,
  parents: List<Unit> = List.Nil(),
): void {
  if (unit_graph.containsKey(unit)) {
    if (parents.contains(unit)) {
      invariant_violation(
        `Dependency cycle found: ` +
          parents.map(u -> u.pkg.name()).reversed().join(" -> "),
      );
      // TODO: Proper error message with exit code 2.
    };
    return void
  };

  unit_deps = compute_deps(unit, resolved_packages);
  unit_graph![unit] = unit_deps;
  for (dep in unit_deps) {
    build_unit_dependencies_for_unit(
      dep,
      resolved_packages,
      unit_graph,
      List.Cons(unit, parents),
    )
  }
}

private fun compute_deps(
  unit: Unit,
  resolved_packages: Map<String, Package>,
): Array<Unit> {
  if (
    unit.target.kind is CustomBuildTarget _ &&
    unit.mode is CompileModeRunBuildScript _
  ) {
    return Array[
      unit with {arch => TargetArchHost(), mode => CompileModeBuild()},
    ]
  };

  assert(unit.mode is CompileModeBuild _);

  unit_deps = mutable Vector[];

  unit.target.kind match {
  | BinTarget _
  | TestTarget _ ->
    // TODO: Allow bin without lib.
    unit_deps.push(
      Unit(
        unit.pkg,
        unit.pkg.manifest.lib_target().fromSome(),
        unit.arch,
        CompileModeBuild(),
      ),
    )
  | LibTarget _ ->
    unit.pkg.manifest.build_script_target() match {
    | Some(target) ->
      unit_deps.push(
        Unit(unit.pkg, target, unit.arch, CompileModeRunBuildScript()),
      )
    | None() -> void
    }
  | _ -> void
  };

  deps = unit.pkg.manifest.dependencies.filter(dep ->
    unit.target.kind match {
    // Binary targets always depend on the lib target, which pulls in the
    // dependencies transitively.
    | BinTarget _ -> false
    | LibTarget _ -> dep.kind is NormalDep _
    | TestTarget _ -> dep.kind is DevelopmentDep _
    | CustomBuildTarget _ -> dep.kind is BuildDep _
    }
  );
  for (dep in deps) {
    dep_package = resolved_packages[dep.name];
    unit_deps.push(
      Unit(
        dep_package,
        dep_package.manifest.lib_target().fromSome(),
        unit.arch,
        CompileModeBuild(),
      ),
    )
  };

  unit_deps.collect(Array)
}

module end;
