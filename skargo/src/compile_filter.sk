module Skargo;

base class FilterRule {
  children =
  | FilterRuleAll()
  | FilterRuleJust(Array<String>)

  static fun create(targets: Array<String>, all: Bool): FilterRule {
    if (all) {
      FilterRuleAll()
    } else {
      FilterRuleJust(targets)
    }
  }

  fun matches(target: Target): Bool
  | FilterRuleAll() -> true
  | FilterRuleJust(targets) -> targets.any(name -> name == target.name)

  fun is_specific(): Bool
  | FilterRuleAll() -> false
  | FilterRuleJust(targets) -> !targets.isEmpty()
}

base class LibRule {
  children =
  /// Include the library, fail if not present
  | LibRuleTrue()
  /// Include the library if present
  | LibRuleDefault()
  /// Exclude the library
  | LibRuleFalse()
}

base class CompileFilter {
  children =
  | CompileFilterDefault()
  | CompileFilterOnly{
    all_targets: Bool,
    lib: LibRule,
    bins: FilterRule,
    tests: FilterRule,
  }

  static fun create{
    rule_lib: LibRule,
    rule_bins: FilterRule,
    rule_tests: FilterRule,
  }: CompileFilter {
    if (
      rule_lib is LibRuleTrue _ ||
      rule_bins.is_specific() ||
      rule_tests.is_specific()
    ) {
      CompileFilterOnly{
        all_targets => false,
        lib => rule_lib,
        bins => rule_bins,
        tests => rule_tests,
      }
    } else {
      CompileFilterDefault()
    }
  }

  static fun from_raw_arguments(
    lib_only: Bool,
    bins: Array<String>,
    all_bins: Bool,
    all_tests: Bool,
    all_targets: Bool,
  ): CompileFilter {
    if (all_targets) {
      return CompileFilterDefault()
    };
    rule_lib = if (lib_only) LibRuleTrue() else LibRuleFalse();
    rule_bins = FilterRule::create(bins, all_bins);
    rule_tests = FilterRule::create(Array[], all_tests);

    static::create{rule_lib, rule_bins, rule_tests}
  }
}

module end;
